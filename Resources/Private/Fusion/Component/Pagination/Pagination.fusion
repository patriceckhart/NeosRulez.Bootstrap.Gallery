prototype(NeosRulez.Bootstrap.Gallery:Component.Pagination) < prototype(Neos.Fusion:Component) {

    pages = ${this.assetsLength && this.itemsPerPage ? Math.ceil(String.toInteger(this.assetsLength) / String.toInteger(this.itemsPerPage)) : false}
    page = ${request.arguments.page ? request.arguments.page : '1'}

    documentUri = Neos.Neos:NodeUri {
        absolute = true
    }

    renderer = afx`
        <nav @if.render={props.pages && props.usePagination ? true : false} id={props.paginationIdentifier} aria-label={props.paginationIdentifier} data-page={props.page} data-pages={props.pages} data-length={props.assetsLength}>
            <ul class="pagination">
                <li @if.render={String.toInteger(props.page) != 1 ? true : false} class="page-item">
                    <a class="page-link" href={String.replace(props.documentUri, '.html', '') + '/page/' + (String.toInteger(props.page) - 1)} aria-label="Previous">
                        <NeosRulez.Bootstrap.Gallery:Component.Pagination.Chevron alignment="left" />
                    </a>
                </li>
                <Neos.Fusion:Loop items={Array.range(1, String.toInteger(props.pages), 1)} itemName="page">
                    <li class={'page-item' + (String.toInteger(props.page) == String.toInteger(page) ? ' active' : '')} >
                        <a class="page-link" href={String.replace(props.documentUri, '.html', '') + '/page/' + page}>{page}</a>
                    </li>
                </Neos.Fusion:Loop>
                <li @if.render={String.toInteger(props.page) < String.toInteger(props.pages) ? true : false} class="page-item">
                    <a id="next" class="page-link" href={String.replace(props.documentUri, '.html', '') + '/page/' + (String.toInteger(props.page) + 1)} aria-label="Next">
                        <NeosRulez.Bootstrap.Gallery:Component.Pagination.Chevron alignment="right" />
                    </a>
                </li>
            </ul>
        </nav>
    `
}
